#region Prolog

###################################
#   Created By Stuart King.                                    
#   IBM Planning Analyics Offering Manager         
#   stuart.king@ca.ibm.com
#   December 2018
###################################

vNow = NumberToString( NOW );

#exportDir = CellGETS('diag.control.cube', 'export directory', 'value');

fullFileName = pDirectory | pElementName | '.csv';

sourceViewName = pElementName | vNow;
IF ( ViewExists ( pCubeName, sourceViewName ) = 1 );
   ViewDestroy ( pCubeName, sourceViewName );
ENDIF;
ViewCreate ( pCubeName, sourceViewName, 1 );

i = 1;
dimensionName = TABDIM ( pCubeName, i );
WHILE ( dimensionName @<> '' );
   i = i + 1;
   dimensionName = TABDIM ( pCubeName, i );
   IF ( i > 10 );
      PROCESSQUIT;
   ENDIF;
END;
dimensionCount = i - 1;

i = 1;
WHILE ( i <= dimensionCount );
   currentDim = TABDIM ( pCubeName, i );
   IF ( currentDim  @= pDimName);
        IF (SubsetExists ( currentDim, sourceViewName ) = 1 );
         SubsetDestroy ( currentDim, sourceViewName ); 
      ENDIF;
      SubsetCreate ( currentDim, sourceViewName, 1 );
      SubsetElementInsert( currentDim, sourceViewName, pElementName, 1 );
      ViewSubsetAssign ( pCubeName, sourceViewName, currentDim, sourceViewName );
   ELSE;
      subsetName = 'temp_populate_cube_' | vNow;
      IF (SubsetExists ( currentDim, subsetName ) = 1 );
         SubsetDestroy ( currentDim, subsetName ); 
      ENDIF;
      SubsetCreate ( currentDim, subsetName, 1 );
      j = 1;
      WHILE ( j <= DIMSIZ (currentDim) );
         currentElement = DIMNM ( currentDim, j );
         IF ( ELLEV (currentDim, currentElement ) = 0);
            SubsetElementInsert (  currentDim, subsetName, currentElement, SubsetGetSize ( currentDim, subsetName ) + 1 );
         ENDIF;
         j = j + 1;
      END; 
   ViewSubsetAssign ( pCubeName, sourceViewName, currentDim, subsetName);
   ENDIF;
   i = i + 1;
END;

ViewExtractSkipZeroesSet ( pCubeName, sourceViewName, 0 );
ViewExtractSkipCalcsSet ( pCubeName, sourceViewName, 1);
ViewConstruct ( pCubeName, sourceViewName );

DataSourceType='VIEW';
DatasourceNameForServer=pCubeName;
DatasourceCubeview=sourceViewName;

DatasourceASCIIQuoteCharacter='';
DatasourceASCIIDelimiter=Char(9);
#endregion
#region Data

vValue = RAND * 100;

IF ( dimensionCount = 2 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2 );
   ENDIF;
ELSEIF ( dimensionCount = 3 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3 );
   ENDIF;
ELSEIF ( dimensionCount = 4 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4 );
   ENDIF;
ELSEIF ( dimensionCount = 5 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5 );
   ENDIF;
ELSEIF ( dimensionCount = 6 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5, dim6 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5, dim6 );
   ENDIF;
ELSEIF ( dimensionCount = 7 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7 );
   ENDIF;
ELSEIF ( dimensionCount = 8 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8 );
   ENDIF;
ELSEIF ( dimensionCount = 9 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8, dim9 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8, dim9 );
   ENDIF;
ELSEIF ( dimensionCount = 10);
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8, dim9, dim10 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8, dim9, dim10 );
   ENDIF;
ELSEIF ( dimensionCount = 11 );
   IF (CELLISUPDATEABLE  ( pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8, dim9, dim10, dim11 ) = 1);
      CELLPUTN ( vValue, pCubeName, dim1, dim2, dim3, dim4, dim5, dim6, dim7, dim8, dim9, dim10, dim11 );
   ENDIF;
ENDIF;
#endregion
#region Epilog

IF ( pTM1Version @= 'v11');
  ASCIIOutput( pDirectory | '/' | pElementName | '.end', 'Complete' );
ENDIF;
#endregion